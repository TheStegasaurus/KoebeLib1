plugins {
    id 'com.google.osdetector' version '1.4.0'    
}

configurations {
    copy
}

repositories {
    mavenCentral()
}

// Library dependencies to download
dependencies {
    copy 'org.python:jython-standalone:2.7.0'
    copy 'com.fifesoft:rsyntaxtextarea:2.6.1'
    copy 'org.jogamp.jogl:jogl-all-main:2.3.2'
    copy 'org.jogamp.gluegen:gluegen-rt-main:2.3.2'
    copy ('org.processing:core:3.3.6') {
        transitive = false
    }
}

// Ensure jar directory exists
task chkJar {
    doLast {
        def f = new File('../lib/jar')
        if (!f.exists()) { f.mkdirs() }
    }
}

// Get cpcore.jar on unix
task get_nix(type:Exec) {
    dependsOn chkJar 
    workingDir 'jar'
    commandLine '../getCirclePack.sh'
}

// Get cpcore.jar on windows
task get_win(type:Exec) {
    dependsOn chkJar
    workingDir 'jar'
    commandLine 'powershell', '-c', '../getCirclePack.ps1', '-executionPolicy', 'RemoteSigned'
}

// Get the right CirclePack cpcore.jar
task get {
    if (osdetector.os == 'windows') {
        dependsOn get_win
    }
    else {
        dependsOn get_nix
    }
}

// Only retrieve CirclePack cpcore.jar if not alread done
def cpcore = new File('../lib/jar/cpcore.jar')
get_nix.onlyIf { !cpcore.exists() }
get_win.onlyIf { !cpcore.exists() }

// Copy all dependencies to ./jar
task copy(type:Copy) {
    into './jar'
    from configurations.copy
}

// Aggregate all dependency managment
task all {
    dependsOn copy
    dependsOn get
}
